arg("").securityresources
| where type =~ "microsoft.security/assessments/subassessments"
| where properties.status.code == "Unhealthy"
| extend status = properties.status.code // ACR 상태 추출(Healthy, Unhealthy,NotApplicable)
| extend assessmentKey = split(id, "/", 12)[0] // 스캐너 평가키 추출
| where assessmentKey == "c0b7cfc6-3172-465a-b378-53c7ff2cc0d5" // 평가키 ACR MDVM 기반 조회
| extend ACR_Name = tostring(split(properties.additionalData.artifactDetails.registryHost, ".",0)[0]),
         repositoryName = tostring(properties.additionalData.artifactDetails.repositoryName),
         repositoryDigest = tostring(properties.additionalData.artifactDetails.digest),
         severity = tostring(properties.additionalData.vulnerabilityDetails.severity)
| summarize Total_scan_image=dcount(repositoryDigest), 
            Vulnerabilities = countif(status=="Unhealthy"), 
            Critical = countif(severity=="Critical"),
            High = countif(severity=="High"),
            Medium = countif(severity=="Medium"),
            Low = countif(severity=="Low")
            by ACR_Name, repositoryName
| sort by ACR_Name asc, Vulnerabilities desc
| extend RowNumber = row_number(1, ACR_Name != prev(ACR_Name))
| where RowNumber <= 5 // ACR 이름 기준 Top5 컬럼까지만 출력
| project-away RowNumber 