arg("").securityresources
| where type =~ "microsoft.security/assessments/subassessments"
| extend assessmentKey = split(id, "/", 12)[0] // 스캐너 평가키 추출
| where assessmentKey == "c0b7cfc6-3172-465a-b378-53c7ff2cc0d5" // 평가키 ACR MDVM 기반 조회
| extend status = properties.status.code // ACR 상태 추출(Healthy, Unhealthy,NotApplicable)
| extend ACR_Name = tostring(split(properties.additionalData.artifactDetails.registryHost, ".",0)[0]),
         repositoryName = tostring(properties.additionalData.artifactDetails.repositoryName),
         repositoryDigest = tostring(properties.additionalData.artifactDetails.digest),
         severity = tostring(properties.additionalData.vulnerabilityDetails.severity)
| summarize Unhealthy_repository=dcountif(ACR_Name, status == "Unhealthy"),
            Total_vulerable_image=dcountif(repositoryDigest,status == "Unhealthy"),
            Total_scan_image=dcount(repositoryDigest),
            Total_vulerable=countif(status=="Unhealthy"),
            Critical = countif(severity=="Critical"),
            High = countif(severity=="High"),
            Medium = countif(severity=="Medium"),
            Low = countif(severity=="Low")            
| extend scan_result = strcat(Total_vulerable_image, " / ", Total_scan_image)
| project Unhealthy_repository, scan_result, Total_vulerable, Critical, High, Medium, Low