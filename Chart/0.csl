//Chart//LA//
let startDate = startofday(datetime_utc_to_local(now(), 'Asia/Seoul') - 7d); // 1주전 00:00:00(KST)부터
let endDate = startofday(datetime_utc_to_local(now(), 'Asia/Seoul')) - 1s; // 전일까지 23:59:59(KST)까지
SecurityIncident
| extend KST_Time = datetime_utc_to_local(TimeGenerated, 'Asia/Seoul') // UTC -> KST 변환
| where KST_Time between (startDate .. endDate)
| where Severity != "Informational" // 정보제공 심각도 제외
| summarize arg_max(KST_Time, Severity, Title) by IncidentNumber
//| where not(Title contains "Reflective DLL Loading" or Title contains "Suspicious LDAP Query" or Title contains "Multi-stage incident involving")
| summarize High = dcountif(IncidentNumber, Severity == 'High'),
            Medium = dcountif(IncidentNumber, Severity == 'Medium'),
            Low = dcountif(IncidentNumber, Severity == 'Low')
            by bin(KST_Time, 1d)
| order by KST_Time asc
| extend KST_Time = format_datetime(KST_Time, "yyyy-MM-dd") // 날짜 형식 변경
| extend Total = High + Medium + Low
| summarize 
    KST_Time = make_list(KST_Time),
    High = make_list(High),
    Medium = make_list(Medium),
    Low = make_list(Low),
    Total = make_list(Total)
    // High_Repeat = make_list(High)
