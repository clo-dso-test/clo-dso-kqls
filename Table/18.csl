//Table//LA//
let LastWeek = SecurityIncident // 전주 데이터 가져오기
| where TimeGenerated >= datetime_utc_to_local(startofday(now()) - 14d, 'Asia/Seoul') and TimeGenerated < datetime_utc_to_local(startofday(now()) - 7d, 'Asia/Seoul')
| where Severity != "Informational" // 정보제공 심각도 제외
| summarize LastWeek_Total=count(Title) by Severity, Title
| order by LastWeek_Total desc
| limit 5
| extend LastWeek_Rank=row_number();
let ThisWeek = SecurityIncident // 금주 데이터 가져오기
| where TimeGenerated >= datetime_utc_to_local(startofday(now()) - 7d, 'Asia/Seoul') and TimeGenerated < datetime_utc_to_local(startofday(now()), 'Asia/Seoul')
| where Severity != "Informational" // 정보제공 심각도 제외
| summarize ThisWeek_Total=count(Title) by Severity, Title, Description, ProviderName
| order by ThisWeek_Total desc
| limit 5
| extend ThisWeek_Rank=row_number();
ThisWeek // 금주 데이터를 기준으로 전주 데이터와 조인
| join kind=leftouter (
    LastWeek
) on Title
| extend Rank_Change = case(
    isnotempty(LastWeek_Rank) and ThisWeek_Rank == LastWeek_Rank, "-",             // 동일한 순위일 경우 '-'
    isnotempty(LastWeek_Rank), 
        strcat(
            iff(ThisWeek_Rank - LastWeek_Rank < 0, "+", "-"),                     // 순위 상승/하락 여부
            tostring(abs(ThisWeek_Rank - LastWeek_Rank))                         // 순위 변동 절대값
        ),
    "New"                                                                        // 전주에 없으면 New
)
| project Rank=ThisWeek_Rank, Severity, Title, ProviderName, Total_Alert=ThisWeek_Total, Rank_Change // 최종 컬럼 선택
| order by Total_Alert desc
