//Table//LA//
let startDate = startofday(datetime_utc_to_local(now(), 'Asia/Seoul') - 7d); // 오늘로부터 7일 전 시작 (00:00 KST)
let endDate = startofday(datetime_utc_to_local(now(), 'Asia/Seoul')) - 1s; // 어제의 마지막 순간 (23:59:59 KST)
let syslogData =
   Syslog
   | extend localTime = datetime_utc_to_local(TimeGenerated, 'Asia/Seoul') // 한국 시간으로 변환
   | where _IsBillable == true
   | where localTime between (startDate .. endDate) // 7일 전 00:00부터 어제 23:59:59까지
   | extend DeviceVendor = 
       case(
           Facility == "local0", "fortiAnalyzer-400Ev",
           Facility == "local1", "다우오피스 TMSe",
           Computer in ("172.16.10.171", "172.16.10.174", "172.16.39.8"), "Cisco",
           tostring(Computer) contains "HDPSV", "Veeam",
           Facility == "user" and Computer in ("Send", "Feb"), "NAC",
           Facility == "user" and Computer == "ESET-MANAGE", "ESET",
           Facility == "user" and Computer == "211.194.150.119", "IPS",
           "Unknown"
       )
   | where DeviceVendor != "Unknown"
   | summarize totalGB = sum(_BilledSize) / 1000000000, 
               minLocalTime = min(localTime), 
               maxLocalTime = max(localTime) 
           by DeviceVendor
   | extend dateRange = strcat(format_datetime(minLocalTime, 'yyyy-MM-dd HH:mm:ss'), " ~ ", format_datetime(maxLocalTime, 'yyyy-MM-dd HH:mm:ss')),
            Type = "Syslog"
   | project dateRange, DeviceVendor, totalGB, Type;
syslogData
| project  DeviceVendor, totalGB = strcat(tostring(round(totalGB, 3)), " GB")
