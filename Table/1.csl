//Table//LA//
arg("").securityresources
        | where type == "microsoft.security/assessments"
        | extend source = trim(' ', tolower(tostring(properties.resourceDetails.Source)))
        | extend resourceId = trim(' ', tolower(tostring(case(
                 source =~ "azure", properties.resourceDetails.Id,
                 source =~ "aws" and isnotempty(tostring(properties.resourceDetails.ConnectorId)), properties.resourceDetails.Id,
                 source =~ "gcp" and isnotempty(tostring(properties.resourceDetails.ConnectorId)), properties.resourceDetails.Id,
                 source =~ 'aws', properties.resourceDetails.AzureResourceId,
                 source =~ 'gcp', properties.resourceDetails.AzureResourceId,
                 extract('^(.+)/providers/Microsoft.Security/assessments/.+$',1,id)
                 ))))
        | extend status = trim(" ", tostring(properties.status.code))
        | extend cause = trim(" ", tostring(properties.status.cause))
        | extend assessmentKey = tostring(name)
        | where assessmentKey == "06e3a6db-6c0c-4ad9-943f-31d9d73ecf6c"
        | summarize Agent_install=toreal(countif(status=="Healthy")), // 에이전트 설치됨
                    Agent_Not_install=toreal(countif(status=="Unhealthy")), // 에이전트 설치안됨
                    NotApplicable = toreal(countif(status=="NotApplicable")), //해당없음
                    Total=count()
        | extend install_rate=strcat(round((Agent_install/(Total-NotApplicable))*100, 1), "%") // 에이전트 설치율
        | project install_rate, Agent_install, Agent_Not_install, NotApplicable, Total