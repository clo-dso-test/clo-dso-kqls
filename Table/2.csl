//Table//LA//
arg("").securityresources
        | where type == "microsoft.security/assessments"
        | extend source = trim(' ', tolower(tostring(properties.resourceDetails.Source)))
        | extend resourceId = trim(' ', tolower(tostring(case(
                 source =~ "azure", properties.resourceDetails.Id,
                 source =~ "aws" and isnotempty(tostring(properties.resourceDetails.ConnectorId)), properties.resourceDetails.Id,
                 source =~ "gcp" and isnotempty(tostring(properties.resourceDetails.ConnectorId)), properties.resourceDetails.Id,
                 source =~ 'aws', properties.resourceDetails.AzureResourceId,
                 source =~ 'gcp', properties.resourceDetails.AzureResourceId,
                 extract('^(.+)/providers/Microsoft.Security/assessments/.+$',1,id)
                 ))))
        | extend status = trim(" ", tostring(properties.status.code))
        | extend cause = trim(" ", tostring(properties.status.cause))
        | extend assessmentKey = tostring(name)
        | where assessmentKey == "dc5357d0-3858-4d17-a1a3-072840bff5be"
        | summarize Healthy=toreal(countif(status=="Healthy")), // 정상
                    Unhealthy=toreal(countif(status=="Unhealthy")), // 비정상
                    NotApplicable = toreal(countif(status=="NotApplicable")), //해당없음
                    Total=count()
        | extend rate=strcat(round((Healthy/(Total-NotApplicable))*100, 1), "%") // 준수율
        | project rate, Healthy, Unhealthy, NotApplicable, Total
        | where rate != "NaN%" // NaN% 인 경우 제외
