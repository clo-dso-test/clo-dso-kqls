//Table//LA//
arg("").securityresources
| where type =~ "microsoft.security/assessments/subassessments"
| where properties.status.code == "Unhealthy"
| extend assessmentKey = split(id, "/", 12)[0] // 스캐너 평가키 추출
| where assessmentKey == "c609cf0f-71ab-41e9-a3c6-9a1f7fe1b8d5" //AKS MDVM 기반 조회
| extend severity = tostring(properties.additionalData.vulnerabilityDetails.severity), //심각도 추출
         imageName =  tostring(split(properties.resourceDetails.id, "@", 0)[0]), //이미지명 추출
         podName = tostring(properties.additionalData.kubernetesContext.workloads[0].ownedResources[0].name), //Pod명 추출
         clusterName = tostring(properties.additionalData.clusterDetails.clusterName) //AKS 클러스터명 추출
| summarize Total_vulnerable_pod=dcount(podName), 
            SeverityTotal = count(), 
            Critical = countif(severity=="Critical"),
            High = countif(severity=="High"),
            Medium = countif(severity=="Medium"),
            Low = countif(severity=="Low") by subscriptionId, clusterName
| order by clusterName asc