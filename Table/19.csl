//Table//LA//
let TI_ip_count = SecurityAlert
    | where TimeGenerated >= datetime_utc_to_local(startofday(now()) - 7d, 'Asia/Seoul') and TimeGenerated < datetime_utc_to_local(startofday(now()), 'Asia/Seoul')  // 최근 7일 동안의 데이터 필터링
    | extend EntityArray = parse_json(Entities) // Entities 필드를 JSON 배열로 변환
    | mv-expand EntityArray // JSON 배열을 각 요소로 확장
    | extend
        EntityType = tostring(EntityArray.Type),
        IPAddress = tostring(EntityArray.Address) // Type과 Address 값 추출
    | where EntityType == "ip" // Type이 'ip'인 경우만 필터링
    | summarize Alerts = count() by IPAddress; // IP별로 발생한 경고 수 집계
TI_ip_count
| join kind=inner (
    ThreatIntelligenceIndicator
    | project
        TI_IP_Address = NetworkSourceIP,
        ThreatType,
        Description,
        Source = SourceSystem,
        ConfidenceScore// ThreatIntelligenceIndicator에서 필요한 컬럼 추출
    )
    on $left.IPAddress == $right.TI_IP_Address // TI IP 주소와 경고 IP 주소를 매칭
| project TI_IP_Address, ThreatType, Source, Alerts // 최종 컬럼 출력
| distinct TI_IP_Address, ThreatType, Source, Alerts
| extend ip_location = geo_info_from_ip_address(TI_IP_Address).country
| where ThreatType != "WatchList"
| order by Alerts desc
| extend Rank=row_number()
| project Rank, TI_IP_Address, ip_location, ThreatType, Alerts
| limit 5
